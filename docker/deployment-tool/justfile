# Justfile for Avatar Deployment Tool
# Run `just` or `just --list` to see available commands

# Show available commands
default:
    @just --list

# Install dependencies
install:
    uv sync --all-extras

# Run all tests (excluding integration tests by default)
test *ARGS:
    uv run pytest tests/unit {{ARGS}}

# Run integration tests only
test-integration *ARGS:
    uv run pytest tests/integration {{ARGS}}

# Run all tests including integration tests
test-all *ARGS:test test-integration

update-test-fixtures:
    AVATAR_DEPLOY_UPDATE_FIXTURES=1 pytest tests/integration/

# Lint code with ruff
lint: format
    uv run ruff check src/ tests/

# Format code with black
format:
    uv run ruff format src/ tests/

# Format and lint
check: format lint

# Build the package
build:
    uv build

# Run the tool interactively in a test directory
run-interactive-github:
    #!/usr/bin/env bash
    TEST_DIR=$(mktemp -d)
    echo "Running in temporary directory: $TEST_DIR"
    uv run octopize-avatar-deploy --output-dir "$TEST_DIR" --verbose
    echo "\nGenerated files in: $TEST_DIR"
    ls -la "$TEST_DIR"

run-interactive-local:
    #!/usr/bin/env bash
    TEST_DIR=$(mktemp -d)
    echo "Running in temporary directory: $TEST_DIR"
    uv run octopize-avatar-deploy --output-dir "$TEST_DIR" --template-from .. --verbose
    echo "\nGenerated files in: $TEST_DIR"
    ls -la "$TEST_DIR"

# Check types with mypy
typecheck:
    uv run mypy src/

# Run the help command
help:
    uv run octopize-avatar-deploy --help

# Show defaults.yaml content
show-defaults:
    @cat src/octopize_avatar_deploy/defaults.yaml
